plugins {
    id 'java'
    id 'war'
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

ext {
    springBootVersion = "2.3.1.RELEASE"
}



allprojects {
    group 'com.windcoder'
    version '1.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'war'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    [compileJava,compileTestJava,javadoc]*.options*.encoding = 'UTF-8'

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
    }

    repositories {
        maven{url 'http://maven.aliyun.com/nexus/content/groups/public/'}
        maven{url 'https://mvnrepository.com/'}
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        compile fileTree(dir: 'lib', exclude:'', include: '*.jar')
//        implementation 'org.springframework.boot:spring-boot-starter'
//        testImplementation('org.springframework.boot:spring-boot-starter-test') {
//            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
//        }
    }

}


subprojects {

    sourceSets {
        main {
            java{
                srcDir 'src/main/java'
            }
            resources {
                srcDir 'src/main/java'
                srcDir 'src/main/resources'
            }
        }
    }

    dependencies {
        annotationProcessor 'org.projectlombok:lombok:1.18.12'
        compileOnly 'org.projectlombok:lombok:1.18.12'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
        testCompileOnly 'org.projectlombok:lombok:1.18.12'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

//    bootRepackage.enabled=false
    jar.enabled = true
    bootJar {
        enabled = false
    }
    bootWar {
        enabled = false
    }
}



configure(rootProject) {
    description "QY CMS"

    project.ext.baseName = 'qy-cms'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven'

//    bootRepackage.enabled=true

    springBoot {
        mainClassName = "com.windcoder.qycms.QycmsApplication";
        buildInfo()
//        executable = true
//        embeddedLaunchScriptProperties(mode:"service")
    }
    dependencies {
        compile project(":qycms-core:system-console")
        compile project(":qycms-blog:blog-console")
        compile project(":qycms-blog:blog-web")
    }

    ext {
        //环境区分，默认开发环境dev,  可选环境 prod，test,uat
        env = System.getProperty("env")?:"dev"
    }

    def embeddedProperties = [ mode:"service" ]
    bootWar {
        enabled = true
        launchScript {
            properties embeddedProperties
        }
    }

    // 生成war时过滤内嵌tomcat依赖
    war {
        rootSpec.exclude("tomcat-embed*.jar")
        rootSpec.exclude("javax.servlet-api*.jar")
        rootSpec.exclude("org.projectlombok:lombok")
    }
}
