buildscript {
    ext {
        springBootVersion = "1.5.8.RELEASE"
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/plugins-release" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}


allprojects {
    group 'com.windcoder'
    version '1.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'war'
    apply plugin: 'org.springframework.boot'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    [compileJava,compileTestJava,javadoc]*.options*.encoding = 'UTF-8'

    configurations {
        providedRuntime
    }

    repositories {
        maven{url 'http://maven.aliyun.com/nexus/content/groups/public/'}
        maven{url 'https://mvnrepository.com/'}
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        compile fileTree(dir: 'lib', exclude:'', include: '*.jar')
    }

}


subprojects {

    sourceSets {
        main {
            java{
                srcDir 'src/main/java'
            }
            resources {
                srcDir 'src/main/java'
                srcDir 'src/main/resources'
            }
        }
    }
    jar.enabled = true
    bootRepackage.enabled=false
}



configure(rootProject) {
    description "QY CMS"

    project.ext.baseName = 'qy-cms'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven'

    bootRepackage.enabled=true

    springBoot {
        mainClass = "com.windcoder.qycms.QycmsApplication";
        buildInfo()
        executable = true
        embeddedLaunchScriptProperties(mode:"service")
    }
    dependencies {
        compile project(":qycms-core:system-console")
        compile project(":qycms-blog:blog-console")
        compile project(":qycms-blog:blog-web")
    }

    // 生成war时过滤内嵌tomcat依赖
    war {

        rootSpec.exclude("tomcat-embed*.jar")
        rootSpec.exclude("javax.servlet-api*.jar")
        rootSpec.exclude("org.projectlombok:lombok")
    }
}
